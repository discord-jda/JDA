/*
 *     Copyright 2015-2018 Austin Keener & Michael Ritter & Florian Spie√ü
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//to build everything:             "gradlew build"
//to build and upload everything:  "gradlew bintrayUpload"

import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.jfrog.bintray' version '1.8.1'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.ben-manes.versions' version '0.19.0'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

def versionObj = new Version(major: 3, minor: 8, revision: 1)

group = "net.dv8tion"
archivesBaseName = "JDA"
version = "$versionObj"

sourceCompatibility = targetCompatibility = 1.8

repositories {
    jcenter()
}

sourceSets {
    examples {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

dependencies {
    /* ABI dependencies */

    //Code safety
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'org.jetbrains:annotations:16.0.1'

    //Logger
    api 'org.slf4j:slf4j-api:1.7.25'

    //Web Connection Support
    api 'com.neovisionaries:nv-websocket-client:2.5'
    api 'com.squareup.okhttp3:okhttp:3.11.0'

    //Opus library support
    api ('club.minnced:opus-java:1.0.4@pom') {
        transitive = true
    }

    /* Internal dependencies */

    //General Utility
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.json:json:20160810'
    implementation 'net.sf.trove4j:trove4j:3.0.3'

    //Sets the dependencies for the examples
    examplesCompile configurations.apiElements
    examplesRuntime configurations.implementation
}

task sourcesForRelease(type: Copy) {
    from ('src/main/java') {
        include '**/JDAInfo.java'
        filter(ReplaceTokens, tokens: [
            versionMajor:    versionObj.major,
            versionMinor:    versionObj.minor,
            versionRevision: versionObj.revision,
            versionBuild:    versionObj.build
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'JDAInfo.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source

    dependsOn generateJavaSources
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

compileTestJava.enabled = false
processTestResources.enabled = false

jar {
    baseName = project.name
    manifest.attributes 'Implementation-Version': version
}

shadowJar.classifier = 'withDependencies'

task noOpusJar(type: ShadowJar, dependsOn: shadowJar) {
    classifier = shadowJar.classifier + '-no-opus'

    configurations += shadowJar.configurations
    from sourceSets.main.output
    exclude 'natives/**'     // ~2 MB
    exclude 'com/sun/jna/**' // ~1 MB
    exclude 'club/minnced/opus/util/*'
    exclude 'tomp2p/opuswrapper/*'

    manifest {
        inheritFrom jar.manifest
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from ('src/main/java') {
        exclude '**/JDAInfo.java'
    }
    from sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
    options.addStringOption('tag', 'incubating:a:Incubating:')

    //### excludes ###
    //jda internals
    exclude('net/dv8tion/jda/bot/entities/impl')
    exclude('net/dv8tion/jda/bot/handle')
    exclude('net/dv8tion/jda/bot/managers/impl')

    exclude('net/dv8tion/jda/client/entities/impl')
    exclude('net/dv8tion/jda/client/handle')
    exclude('net/dv8tion/jda/client/managers/impl')

    exclude('net/dv8tion/jda/core/audio/AudioConnection.java',
            'net/dv8tion/jda/core/audio/AudioWebSocket.java',
            'net/dv8tion/jda/core/audio/AudioPacket.java',
            'net/dv8tion/jda/core/audio/Decoder.java',
            'net/dv8tion/jda/core/audio/hooks/ListenerProxy.java',
            'net/dv8tion/jda/core/entities/impl',
            'net/dv8tion/jda/core/entities/EntityBuilder.java',
            'net/dv8tion/jda/core/handle',
            'net/dv8tion/jda/core/managers/impl',
            'net/dv8tion/jda/core/requests/GuildLock.java',
            'net/dv8tion/jda/core/requests/WebSocketClient.java',
            'net/dv8tion/jda/core/requests/RateLimiter.java',
            'net/dv8tion/jda/core/requests/Request.java',
            'net/dv8tion/jda/core/requests/Route.java',
            'net/dv8tion/jda/core/requests/Requester.java',
            'net/dv8tion/jda/core/requests/Response.java',
            'net/dv8tion/jda/core/requests/ratelimit',
            'net/dv8tion/jda/core/utils/cache/impl',
            'net/dv8tion/jda/core/utils/JDALogger.java',
            'net/dv8tion/jda/core/utils/Checks.java',
            'net/dv8tion/jda/core/utils/Helpers.java',
            'net/dv8tion/jda/core/utils/concurrent',
            'net/dv8tion/jda/core/utils/tuple')

    //voice crypto
    exclude('com/iwebpp/crypto')
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = 'JDA'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/DV8FromTheWorld/JDA.git'
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

String getProjectProperty(String propertyName) {
    String property = ""
    if (hasProperty(propertyName)) {
        property = this.properties[propertyName]
    }
    return property
}

build {
// clean prevents incremental compilation. should only happen on bintrayUpload
//    dependsOn clean
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar
    dependsOn shadowJar
    dependsOn noOpusJar

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

bintrayUpload {
    dependsOn clean
    dependsOn build
    build.mustRunAfter clean

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
    onlyIf { System.getenv("BUILD_NUMBER") }
}

class Version {
    String major, minor, revision

    static String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?:
        System.getenv("GIT_COMMIT")?.substring(0, 7) ?: System.getProperty("GIT_COMMIT")?.substring(0, 7) ?:"DEV"
    }

    String toString() {
        "$major.$minor.${revision}_$build"
    }
}
